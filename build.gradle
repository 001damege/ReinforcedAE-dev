buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

apply plugin: 'org.spongepowered.mixin'

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id.replace(" ", "")
}


java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
            property 'guideme.pccard.guide.sources', file('guidebook').absolutePath
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/main/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"

    config "${mod_id}.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Modmaven"
        url = "https://modmaven.dev/"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // JEI
    compileOnly fg.deobf("curse.maven:jei-238222:6075247")
    runtimeOnly fg.deobf("curse.maven:jei-238222:6075247")

    // Relations
    implementation fg.deobf("curse.maven:glodium-957920:5226922") // Glodium
    implementation fg.deobf("curse.maven:guideme-1173950:6489464") // GuideME
    implementation fg.deobf("curse.maven:cloth-config-348521:5729105") // Cloth Config API
    implementation fg.deobf("curse.maven:curios-309927:6418456") // Curios API
    implementation fg.deobf("curse.maven:geckolib-388172:6333474") // GeckoLib
    implementation fg.deobf("curse.maven:patchouli-306770:6164575") // Patchouli

    // AE2 + Addon
    implementation fg.deobf("curse.maven:applied-energistics-2-223794:6501484") // Applied Energistics 2
    implementation fg.deobf("curse.maven:ex-pattern-provider-892005:6518515") // Extended AE
    implementation fg.deobf("curse.maven:mega-cells-622112:6175008") // MEGA Cells
    implementation fg.deobf("curse.maven:advancedae-1084104:6205290") // Advanced AE
    implementation fg.deobf("curse.maven:applied-flux-965012:6490472") // Applied Flux
    implementation fg.deobf("curse.maven:ars-energistique-905641:5504444") // Ars Ã‰nergistique
    implementation fg.deobf("curse.maven:applied-botanics-addon-610632:4904185") // Applied Botanics Addon
    implementation fg.deobf("curse.maven:applied-mekanistics-574300:4842281") // Applied Mekanistics
    implementation fg.deobf("curse.maven:ae2-things-forge-609977:4616683") // AE2 Things

    // Mekanism
    implementation fg.deobf("curse.maven:mekanism-268560:6258796") // Mekanism

    // Ars Nouveau
    implementation fg.deobf("curse.maven:ars-nouveau-401955:5894609") // Ars Nouveau

    // Botania
    implementation fg.deobf("curse.maven:botania-225643:6366547") // Botania

    // other
    runtimeOnly fg.deobf("curse.maven:jade-324717:6271651") // Jade
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version       : forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors         : mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    finalizedBy 'reobfJar'
}


publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
